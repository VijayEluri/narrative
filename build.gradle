apply plugin: 'java'
apply plugin: 'eclipse'

defaultTasks 'clean', 'test', 'jar'

group = "com.youdevise"
ext.majorVersion=1
ext.minorVersion=0

targetCompatibility = 1.5

def isPrivateRelease() {
    return System.getenv('BUILD_NUMBER') != null
}

if (isPrivateRelease()) {
    def buildNumber = System.getenv('BUILD_NUMBER')
    version = "${ext.majorVersion}.${ext.minorVersion}.${buildNumber}"
} else /* dev build */ {
    def timestamp = new Date().format('yyyyMMdd-HHmmss', TimeZone.getTimeZone('UTC'))
    version = "${ext.majorVersion}.${ext.minorVersion}-${timestamp}"
}

repositories {
    mavenLocal()
    if (project.hasProperty('repoUrl')) {
        mavenRepo url: "${project.repoUrl}/groups/public"
    }
    else {
        mavenCentral()
    }
}
    
dependencies {
    compile      group: 'org.hamcrest',  name: 'hamcrest-integration',    version: '1.2'
    
    testCompile  group: 'org.jmock',     name: 'jmock',           version: '2.5.1'
    testCompile  group: 'org.jmock',     name: 'jmock-junit4',    version: '2.5.1'
    testCompile  group: "junit",         name: 'junit-dep',       version: "4.8.2"
}

configurations {
    testCompile.exclude module: "hamcrest"
}

task packageSources(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
 
artifacts {
    archives jar
    archives packageSources
}

